apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
        applicationId "org.traccar.client"
        buildConfigField "boolean", "HIDDEN_APP", "false"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 51
        versionName '5.14'
    }

    lintOptions {
        checkReleaseBuilds false
    }

    flavorDimensions "default"
    productFlavors {
        regular
        google
        hidden {
            applicationId "org.traccar.client.hidden"
            buildConfigField "boolean", "HIDDEN_APP", "true"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation 'com.android.support:design:28.0.0-rc02'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.8'
    googleImplementation 'com.google.firebase:firebase-core:16.0.3'
    googleImplementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Google")) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'io.fabric'

    task copyJson(type: Copy) {
        from '../../traccar-shared/google-services'
        into '.'
        include 'traccar-client-android.json'
        rename('traccar-client-android.json', 'google-services.json')
    }

    afterEvaluate {
        preBuild.dependsOn copyJson
    }
}
