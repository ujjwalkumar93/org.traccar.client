apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "org.traccar.client"
        buildConfigField "boolean", "HIDDEN_APP", "false"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 51
        versionName '1.1'
        multiDexEnabled true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }


    lintOptions {
        checkReleaseBuilds false
    }

    flavorDimensions "default"
    productFlavors {
        regular
        google
        hidden {
            applicationId "org.traccar.client.hidden"
            buildConfigField "boolean", "HIDDEN_APP", "true"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    //noinspection GradleCompatible
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'com.android.support:design:28.0.0-rc02'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:28.0.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.8'
    googleImplementation 'com.google.firebase:firebase-core:16.0.3'
    googleImplementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'
    implementation 'com.codepath.libraries:android-oauth-handler:1.3.1'
    implementation 'org.scribe:scribe:1.3.5'
    implementation 'com.google.apis:google-api-services-oauth2:v1-rev145-1.25.0'
    implementation 'com.android.support:multidex:1.0.3'

    


    // implementation 'com.google.android.gms:play-services:12.0.1'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Google")) {
    apply plugin: 'com.google.gms.google-services'
    //implementation 'com.google.android.gms:play-services:12.0.1'
    apply plugin: 'io.fabric'

    task copyJson(type: Copy) {
        from '../../traccar-shared/google-services'
        into '.'
        include 'traccar-client-android.json'
        rename('traccar-client-android.json', 'google-services.json')
    }

    afterEvaluate {
        preBuild.dependsOn copyJson
    }
}
